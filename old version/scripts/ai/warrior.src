//This AI is for wandering warriors looking for work

use npc;
use os;
use uo;
use cfgfile;

include "include/myutil";
include "include/attributes";
include "include/speech";
include "include/client";
include "include/eventid";
include "include/randname";
include "include/anchors";
include "include/npcbackpacks";
include "include/dotempmods";
include "include/myutil";
include "include/yesno";
include "include/random";
include "ai/setup/modsetup";

const HALT_THRESHOLD := 10;

const DRINK_SOUND := 0x32;
const SOUND_EFFECT_HEAL	:= 0x1f3;
const SOUND_EFFECT_CURE	:= 0x1e1;
const EFFECT_SPARKLE  := 0x374a;

var speech := 5;
var me := Self();

var lastbark := ReadGameClock();
var masterserial := 0;
var master;

var chaseloop := 0;
var following := 0;
var guarding := 0;
var nextwatch := ReadGameClock();
var laststrengthgain := ReadGameClock();

program Warrior()

	if( me.name["<random>"] )
		SetMeUp();
	endif

	if (GetObjProperty(me, "frozen"))
		me.frozen := 1;
	endif

	FixStuff();

	masterserial := GetObjProperty(me,"master");
	if ( (masterserial == 0) || (!masterserial) || (masterserial == "<error>") )
		masterserial := 0;
	else
		master := SystemFindObjectBySerial(masterserial);
		if (master)
			me.setmaster(master);
			if (Distance(me, master) < 10)
				following := master;
				guarding := master;
			endif
		endif
	endif

	SetWarMode( 0 );
	Drop_Anchor();

	var ev;
	var next_wander := ReadGameClock() + 10;
	var wanders := 9;

	EnableAllEvents();
	if (masterserial != 0)
		EnableEvents(SYSEVENT_SPEECH,12);
	else
		EnableEvents(SYSEVENT_SPEECH,2);
	endif

	while (1)

		if (me.poisoned)
			ApplyHealing(me);
		elseif ( GetHp(me) < GetMaxHp(me) )
			ApplyHealing(me);
		endif

		ev := WarriorMovement(wanders, next_wander);

		if (ev)
			if (masterserial == 0)
				ProcessMasterlessEvents(ev, wanders, next_wander);
			else
				ProcessMasterEvents(ev, wanders, next_wander);
			endif
		endif

	endwhile

endprogram

function CloseDistance( opponent )

	var archerymovement := HasBow();
	var gd := 0;
	if (guarding) gd := Distance(me,guarding); endif
	var d := Distance(me, opponent);


	if ( gd > 15 )
		guarding := 0;
	elseif ( gd > 8 )
		RunToward( guarding );
		return 2;
	elseif ( gd > 4 )
		if (archerymovement)
			if (d == 1) ChangeWeapons(); endif
			return 1;
		else
			RunToward( guarding );
			return 2;
		endif
	endif

	if ( d > 15 )
		return 2;
	endif

	if (!archerymovement)
		if ( d > 1 )
			RunToward( opponent );
			return 0;
		else
			return 1;
		endif
	else
		if (d == 1)
			ChangeWeapons();
		elseif ( d < 4 )
			RunAwayFrom(opponent);
			return 0;
		else
			return 1;
		endif
	endif

endfunction

function HasBow()

	var ammotype := 0;
	var bowarmed := 0;
	var weapon := me.weapon;
	if (!weapon.objtype)
		say("I don't have a weapon!");
		ChangeWeapons();
		return 0;
	endif
	case (weapon.objtype)
		0x13b2:
			bowarmed := 1;
			ammotype := 0xf3f;//bow and arrows
		0x0f4f:
		0x13fd:
			bowarmed := 1;
			ammotype := 0x1bfb;//xbow and bolts
		0x6050:
			bowarmed := 1;
			ammotype := 0x6051;//firebow and firearrows
		default: return 0;
	endcase
	if (!bowarmed) return 0; endif

	var myammo := EnumerateItemsInContainerOfObjtype(me.backpack, ammotype);

	if (!len(myammo))
		say("No ammo!");
		ChangeWeapons();
		return 0;
	else
		return 1;
	endif

endfunction

function ChangeWeapons()

	var weapon := GetEquipmentByLayer(me,LAYER_HAND1);
	var shield := GetEquipmentByLayer(me,LAYER_HAND2);

	if (weapon)
		MoveItemToLocation(weapon, me.x, me.y, me.z, MOVEITEM_FORCELOCATION);
	endif
	if (shield)
		MoveItemToLocation(shield, me.x, me.y, me.z, MOVEITEM_FORCELOCATION);
	endif
	foreach item in ListRootItemsInContainer(me.backpack)
		EquipItem(me,item);
	endforeach
	if (weapon)
		MoveItemToContainer(weapon, me.backpack);
	endif
	if (shield)
		MoveItemToContainer(shield, me.backpack);
	endif

endfunction

function sleepmode()

	foreach critter in ListMobilesNearLocation( me.x,me.y,me.z, 32 )
          if (!critter.isA(POLCLASS_NPC))
		return 0;
          endif
	endforeach

	var ev;
	while (1)
		ev := os::wait_for_event( 120 );
		repeat
		if (ev)
		case (ev.type)
                	SYSEVENT_ENGAGED:
			SYSEVENT_DAMAGED:
				return ev;
			SYSEVENT_ENTEREDAREA:
			SYSEVENT_SPEECH:
				if (!ev.source.isA(POLCLASS_NPC))
					return ev;
				endif
		endcase
		endif
		until (! (ev := os::wait_for_event(120)) );
	endwhile     

endfunction      

function SetMeUp()

	me.gender := Random(2);
	me.graphic := 400+me.gender;

	me.name := "<random>";
	me.name := RandomName( me );
	me.title_suffix := " the Warrior";

	foreach item in ListEquippedItems(me)
		DestroyItem(item);
	endforeach

	SetBaseStrength(me, Random(50)+10 );
	SetBaseIntelligence(me, Random(50)+10 );
	SetBaseDexterity(me, Random(50)+10 );

	var parms := {};
	parms[1]:= me;
	parms[2]:= "weakwarrior";
	start_script("::/misc/dressme", parms);

	if (!me.backpack)
		var newbackpack := CreateItemAtLocation(5288, 1176, 0, 0xe75,1);
		if (!EquipItem(me, newbackpack)) DestroyItem(newbackpack); endif
	endif

	var i;
	for (i := 0; i <= 48; i := i + 1)
		SetBaseSkillBaseValue(me,i,Random(200)+200);
	endfor

	master := 0;
	masterserial := 0;
	SetObjProperty(me,"master",masterserial);

	SetHp(me, me.maxhp);

endfunction

function ProcessMasterlessEvents(byref ev, byref wanders, byref next_wander)

	case (ev.type)
	SYSEVENT_SPEECH:
		wanders := 0;
		next_wander := ReadGameClock()+60;
		var evtext := lower(ev.text);
		if ( evtext[ lower(me.name) ] )
			TurnToward(ev.source);
			say("Hi there, " + ev.source.name);
		elseif ( evtext[ "join"] )
			TurnToward(ev.source);
			JoinThem(ev.source);
		elseif (!AskToJoin(ev.source))
			check_speech(ev.text, ev.source);
		endif
	SYSEVENT_ENGAGED:
	SYSEVENT_DAMAGED:
		AddToHitList(ev);
		wanders := 0;
		next_wander := ReadGameClock()+60;
		if ( (ev.source) && (!ev.source.dead) )
			Fight( ev.source );
		endif
	SYSEVENT_ENTEREDAREA:
		return;
	SYSEVENT_ITEM_GIVEN:
		wanders := 0;
		next_wander := ReadGameClock()+60;
		TakeItem(ev.source,ev.item);
	endcase

endfunction

function FixStuff()

	me.graphic := 400+me.gender;
	me.title_suffix := " the Warrior";

endfunction

function Fight(opponent)

	var wasfollowing := following;
	following := 0;

	if (GetDexterityMod(me) >= 5000)
		SetDexterityMod(me, GetDexterityMod(me) - 5000);
	endif

	if ((opponent.cmdlevel > 0) || (opponent == me) )
        	SetWarMode(0);
		following := wasfollowing;
		return;
	endif

	if (opponent.npctemplate == "humuc")
		following := wasfollowing;
		SetWarMode(0);
		opponent := 0;
		return;
	endif

	if ( opponent == master )
		say("Wanna spar, eh?  Just tell me when to stop!");
	endif

	var oldprio := set_priority(50);
	SetOpponent( opponent );
	TurnToward( opponent );

	if ( GetObjProperty( me, "keywordbattlecry") )
		say( GetObjProperty( me, "keywordbattlecry") );
	endif

	var ev;

	chaseloop := 0;
	var loops := 1;
	var cd;

	var wasmoving:=1;

    	while ( (opponent) && not
	    (opponent.dead || opponent.hidden || opponent.concealed) &&
	    (Distance(me,opponent) < 20) )

		if (me.poisoned)
			ApplyHealing(me);
		elseif ( GetHp(me) < GetMaxHp(me) )
			ApplyHealing(me);
		elseif (GetHp(me) < 10)
			Flee(opponent);
		endif

		GainStat();

		cd := CloseDistance( opponent );
		case (cd)
		0:
			wasmoving := 1;
			loops := loops + 1;
			if (loops > 25)
				if ( HasBow() ) ChangeWeapons(); endif
				chaseloop := chaseloop + 1;
				loops := 0;
				RunRandom(opponent, chaseloop);
				if (chaseloop > 5)
					sleep(2);
					loops := 0;
					chaseloop := 0;
					Flee(opponent);
				endif
			endif
		1:
			if (wasmoving) TurnToward(opponent); endif
			if (!CheckLineOfSight(me,opponent))
				SetOpponent(0);
				SetWarMode(0);
				following := wasfollowing;
				if ( Distance(following,me)>15 )
					if (following)
						say("Oh crap.  Where is " + following.name + "?");
						following:=0;
					endif
				endif
				return;
			endif
			loops := 0;
			chaseloop := 0;
			wasmoving := 0;
		2:
			SetOpponent(0);
			SetWarMode(0);
			following := wasfollowing;
			if ( Distance(following,me)>15 )
				if (following)
					say("Oh crap.  Where is " + following.name + "?");
					following:=0;
				endif
			endif
			return;
		endcase

		ev := wait_for_event( cd );
		case (ev.type)
		SYSEVENT_SPEECH:
			var evtext := lower(ev.text);
			var mename := lower(me.name);
			if (ev.source.serial == masterserial)
				if ( evtext["stop"] )
					say("Okay");
					SetWarMode(0);
					opponent := 0;
					sleep(1);
					following := wasfollowing;
					if ( Distance(following,me)>15 ) following:=0;endif
					chaseloop := 0;
					return;
				elseif (evtext[mename + " rearm"] )
					say("Okay");
					ChangeWeapons();
					if (HasBow())
						RunAwayFrom(opponent);
						RunAwayFrom(opponent);
						RunAwayFrom(opponent);
						TurnToward( opponent );
					endif
				else
					ObeyCommand(ev);
					if (following)
						SetWarMode(0);
						opponent := 0;
						chaseloop := 0;
						return;
					endif
				endif
			endif
		SYSEVENT_DAMAGED:
		SYSEVENT_ENGAGED:
			if (ev.source)
				AddToHitList(ev);
				if (ev.source != opponent)
					if ( (Random(6)==1) ||
					     (Distance(ev.source,me) < Distance(me, opponent)) )
						opponent := ev.source;
						SetOpponent( opponent );
						TurnToward( opponent );
					endif
				endif
			endif
		EVID_PEACEMADE:
			SetWarMode(0);
			opponent := 0;
			sleep(1);
			following := wasfollowing;
			if ( Distance(following,me)>15 ) following:=0;endif
			chaseloop := 0;
			return;
		endcase

		OpenDoors(opponent);

	endwhile

	SetOpponent(0);
	SetWarMode(0);
	following := wasfollowing;
	if ( Distance(following,me)>15 )
		if (following)
			say("Oh crap.  Where is " + following.name + "?");
			following:=0;
		endif
	endif

endfunction

function AddToHitList(ev)

	if ( (ev.type != SYSEVENT_DAMAGED) || (ev.source.npctemplate) ) return; endif
	var hitlist := GetObjProperty(me,"hitlist");
	if (!hitlist) hitlist := {}; endif
	foreach mob in hitlist
		if (mob == ev.source.serial) return; endif
	endforeach
	hitlist.append(ev.source.serial);
	SetObjProperty(me,"hitlist",hitlist);

endfunction

function Flee(opponent)

	EraseObjProperty(me,"killme");
	if ( (me.frozen) || (me.paralyzed) )
		sleep(1);
		return;
	endif
	var runs := 0;
	var ev;

	say("Aaaaiiiiiii!!!");

	while ( (Distance(me, opponent) < 20) && (runs < 50) )
		RunAwayFrom(opponent);
		runs := runs +1;
		ev := wait_for_event(0);
		if (ev)
			AddToHitList(ev);
		endif
	endwhile

	RestartScript( me );

endfunction

function EnableAllEvents()

	EnableEvents(SYSEVENT_SPEECH,8);
	EnableEvents(SYSEVENT_ITEM_GIVEN + SYSEVENT_ENGAGED + SYSEVENT_DAMAGED);
	EnableEvents( SYSEVENT_DOUBLECLICKED );
	EnableEvents(SYSEVENT_ENTEREDAREA, HALT_THRESHOLD);
	EnableEvents( EVID_PEACEMADE );

endfunction

function AskToJoin(you)

	if ( lastbark > ReadGameClock() ) return 0; endif

	var leadership := GetEffectiveSkill(you, SKILLID_ENTICEMENT);
	if (leadership < (GetStrength(me) - GetStrengthMod(me)) )
		return 0;
	else
		lastbark := ReadGameClock()+60;
		say("I want to be a great warrior someday.");
		sleep(1);
		say("You look like a capable leader.  If you will train me, I would follow you.");
		sleep(1);
		say("Would you like for me to JOIN you?");
		return 1;
	endif

endfunction

function JoinThem(you)

	var leadership := GetEffectiveSkill(you, SKILLID_ENTICEMENT);
	if (leadership < (GetStrength(me) - GetStrengthMod(me)) )
		say("You don't look like much of a leader, to me.");
		return 0;
	endif

	var henchman := GetObjProperty(you,"henchman");
	if (henchman)
		henchman := SystemFindObjectBySerial(henchman);
		if (henchman)
			say("You already have a henchman.");
			return;
		endif
	endif

	SetObjProperty(you,"henchman",me.serial);
	SetObjProperty(me,"master",you.serial);
	masterserial := you.serial;
	master := you;
	me.SetMaster(you);
	following := master;
	guarding := master;

	var i, skill;
	for (i:=0; i <= 48; i := i +1)
		skill := GetEffectiveSkill(you,i);
		if (skill > leadership) skill := leadership; endif
		SetBaseSkillBaseValue(me,i,skill*10);
	endfor

	SetBaseStrength(me, Random(10)+50 );
	if ( GetStrength(me) > leadership ) SetBaseStrength(me, leadership ); endif

	SetBaseIntelligence(me, Random(10)+50 );
	if ( GetIntelligence(me) > leadership ) SetBaseIntelligence(me, leadership ); endif

	SetBaseDexterity(me, Random(10)+50 );
	if ( GetDexterity(me) > leadership ) SetBaseStrength(me, leadership ); endif

	say("Wow, I've learned so much from you already!");

	set_critical(1);
	if ( GetObjProperty( me, "regspawn" ) )
		var spawnname := GetObjProperty( me, "regspawn" );
		var numspawns := CInt(GetGlobalProperty(spawnname))-1;
		SetGlobalProperty(spawnname,numspawns);
		EraseObjProperty( me, "regspawn" );
	endif
	set_critical(0);

	EnableEvents(SYSEVENT_SPEECH,12);

endfunction

function ProcessMasterEvents(byref ev, byref wanders, byref next_wander)

	if ( (!master) && (ev.source.serial == masterserial) )
		master := ev.source;
		me.setmaster(master);
		following := master;
		guarding := master;
	endif

	case (ev.type)
	SYSEVENT_SPEECH:
		wanders := 0;
		next_wander := ReadGameClock()+60;
		var evtext := lower(ev.text);
		if (ev.source.serial == masterserial)
			if (!ObeyCommand(ev))
				check_speech(ev.text, ev.source);
			endif
		else
			check_speech(ev.text, ev.source);
		endif
	SYSEVENT_ENGAGED:
		wanders := 0;
		next_wander := ReadGameClock()+60;
		if ( (ev.source) && (!ev.source.dead) && (!guarding) )
			Fight( ev.source );
		endif
	SYSEVENT_DAMAGED:
		AddToHitList(ev);
		wanders := 0;
		next_wander := ReadGameClock()+60;
		if ( (ev.source) && (!ev.source.dead) )
			Fight( ev.source );
		endif
	SYSEVENT_ENTEREDAREA:
		return;
	EVID_ALL_ATTACK_CMD:
		Fight(ev.target);
	SYSEVENT_ITEM_GIVEN:
		wanders := 0;
		next_wander := ReadGameClock()+60;
		if (ev.source.serial == masterserial)
			say("Thanks, I'll put that in my backpack");
			var it := ev.item;
			MoveItemToContainer( it, me.backpack);
		else
			TakeItem(ev.source,ev.item);
		endif
	SYSEVENT_DOUBLECLICKED:
		if (Random(20)==1) say("hehe"); endif
		if (ev.source.serial == masterserial) OpenMyPack(); endif
	EVID_ALL_FOLLOW_CMD:
		following := ev.target;
	EVID_ALL_GUARD_CMD:
		guarding := ev.target;
	endcase

endfunction

function WarriorMovement(byref wanders, byref next_wander)

	if (masterserial == 0)
		if ( ReadGameClock() >= next_wander )
			wanders:=wanders+1;
			wander();
			next_wander := ReadGameClock() + 10;
			if (wanders>60)
				wanders := 0;
				return (sleepmode());
			else
				return (wait_for_event(5));
			endif
		else
			return (wait_for_event(5));
		endif
	endif

	if ( (following) && (master) )
		SetAnchor( me.x, me.y, 20, 0 );
		Follow();
		return (wait_for_event(0));
	elseif ( (guarding) && (master) )
		SetAnchor( me.x, me.y, 20, 0 );
		Guard();
		return (wait_for_event(7));
	else
		//I'm not following, guarding, my master is offline, and I'm bored:
		return (wait_for_event(120));
	endif

endfunction

function Guard()

	if ( (me.frozen) )
		sleep(10);
		return;
	endif

	if ( nextwatch <= ReadGameClock() )
		nextwatch := ReadGameClock() + 7;
		if ( Random(15)==1 )
			say("I've got your back, " + guarding.name);
			if ( Random(20)==1 ) say("WAY back"); endif
		endif
		foreach mob in ListHostiles( guarding, 9, 0 );
			Fight(mob);
		endforeach
	endif

	if ( (!GetStrength(guarding)) || (guarding.dead) || (Distance(me,guarding) > 15) )
		guarding := 0;
	endif

endfunction

function Follow()

	if ( (me.frozen) )
		sleep(10);
		return;
	endif

	var d := Distance( me, following );
	if (d < 3)
		sleep(1);
		chaseloop := 0;
	elseif (( d > 10 ) || ( chaseloop > 50 ))
		MoveCharacterToLocation( me, following.x, following.y, following.z,
				MOVECHAR_FORCELOCATION );
		chaseloop := 0;
		sleep(1);
	else
		SetDexterityMod(me, GetDexterityMod(me) + 5000);
		RunToward(following);
		chaseloop := chaseloop + 1;
		SetDexterityMod(me, GetDexterityMod(me) - 5000);
	endif

	if ( (!GetStrength(following)) || (following.dead) )
		following := 0;
	endif

	if (GetDexterityMod(me) >= 5000)
		SetDexterityMod(me, GetDexterityMod(me) - 5000);
	endif

	if ( (master) && (!guarding) )
		guarding := master;
	endif

	if (guarding)
		Guard();
	endif

endfunction

function ObeyCommand(ev)

	var mename := lower(me.name);
	var evtext := lower(ev.text);
	var you := ev.source;

	if (!master)
		master := you;
		me.setmaster(you);
		following := master;
		guarding := master;
		say("You da boss");
	endif

	if (master != you)
		say("Funny, I thought you were my master...");
		return 0;
	endif

	if (evtext[mename + " kill"] || evtext[mename + " attack"] ||
	    evtext["all kill"] || evtext["all attack"] )
		var what := Target( you, TGTOPT_HARMFUL + TGTOPT_CHECK_LOS );
		if (what)
			if (evtext["all kill"] || evtext["all attack"] )
				AllCommand(EVID_ALL_ATTACK_CMD, what);
                		Fight( what );
			else
                		Fight( what );
			endif
		endif
	elseif( evtext[mename + " stop"] || evtext["all stop"])
		guarding := 0;
		following := 0;
	elseif( evtext[mename + " heal"] || evtext["heal me"])
		if ( evtext["heal me"] )
			TurnToward(you);
			if (!ApplyHealing(you))
				say("I can't heal again yet.");
			endif
		else
			var what := Target( you, TGTOPT_HELPFUL + TGTOPT_CHECK_LOS );
			TurnToward(what);
			if (!ApplyHealing(what))
				say("I can't heal again yet.");
			endif
		endif
	elseif ((evtext[mename + " come"]) || (evtext["all come"]))
		var done := 0;
		var chaseloop := 0;
		while( done == 0 )
			chaseloop := chaseloop +1;
			done := CloseDistance( you );
			if (chaseloop > 25)
				done :=1;
			endif
		endwhile
	elseif (evtext[mename + " follow"])
		if (evtext["follow me"])
			say("Okay");
			following := you;
		else
			var what := Target( you, TGTOPT_CHECK_LOS );
			if ( (what) && (GetStrength(what)) )
				say("I'll follow " + what.name);
				following := what;
			endif
		endif
	elseif (evtext["all follow"])
		if (evtext["follow me"])
			say("Okay");
			following  := you;
		else
			var what := Target( you, TGTOPT_CHECK_LOS );
			if (what)
				AllCommand(EVID_ALL_FOLLOW_CMD, what);
			endif
		endif
	elseif (evtext[mename + " transfer"])
		TurnToward(you);
		say("Are you talking to me?");
	elseif (evtext[mename + " guard"])
		if (evtext["guard me"])
			guarding := you;
			say("Gotcha covered, " + guarding.name);
		else
			var what := Target( you, TGTOPT_HELPFUL + TGTOPT_CHECK_LOS );
			if (GetHp(what))
				guarding := what;
				say("Gotcha covered, " + guarding.name);
			endif
		endif	
	elseif (evtext["all guard"])
		if (evtext["guard me"])
			guarding := you;
			say("Gotcha covered, " + guarding.name);
		else
			var what := Target( you, TGTOPT_HELPFUL + TGTOPT_CHECK_LOS );
			if (GetHp(what))
				AllCommand(EVID_ALL_GUARD_CMD, what);
			endif
		endif	
	elseif (evtext[mename + " fetch"]  || (evtext[mename + " get"]))
		TurnToward(you);
		say("Are you talking to me?");
	elseif (evtext[mename + " drop"])
		TurnToward(you);
		say("Okay");
		DropEverything();
	elseif (evtext[mename + " dismount"])
		var mount := GetEquipmentByLayer( me, 25 );
		if (mount)
			if ( Dismount(me,mount) )
				say("all transfer");
			endif
		endif	
	elseif ( (evtext[mename + " ride"]) || (evtext[mename + " mount"]) )
		TurnToward(you);
		var alreadymounted := GetEquipmentByLayer( me, 25 );
		if (alreadymounted)
			say("I already have a mount, thanks");
		else
			say("Ride what?");
			var horse := Target(you);
			if (horse)
				MountHorse(horse);
			endif
		endif
	elseif (evtext[mename + " speak"]  || (evtext["all speak"]))
		TurnToward(you);
		say("Are you talking to me?");
	elseif (evtext[mename + " rearm"] )
		say("Okay");
		ChangeWeapons();
	elseif ( evtext[mename + " release"] )
		TurnToward(you);
		say("You're firing me?");
		if (YesNo(you,"Fire them?")) Release(); endif
	elseif ( evtext[mename + " wear"] )
		TurnToward(you);
		say("Wear what?");
		var it := Target(you);
		WearItem(it, you);
	elseif ( evtext == "brb" )
		say("k thx");
		sleep(5);
	elseif ( evtext[mename + " undress"] )
		say("Okeedokee");
		sleep(1);
		Undress();
	else
		return 0;
	endif

	return 1;

endfunction

function AllCommand(evtype,what)

	var mobs := ListMobilesNearLocation( master.x, master.y, master.z, 9 );
	var eve array;
	eve.+type := evtype;
	eve.+source := me;
	eve.+target := what;
	SendEvent( me, eve );
	foreach mob in mobs
		if( GetObjProperty( mob, "master" ) == me.master.serial )
			SendEvent( mob, eve );
		endif
	endforeach

endfunction

function ApplyHealing(who)

	if (Distance(me,who) > 3)
		say(who.name + " is too far away!");
		return 1;
	endif

	if (GetHp(me) < 20) DrinkHeal(); endif

	if (!GetObjProperty(me,"#heal"))
		SetObjProperty(me,"#heal", ReadGameClock()-20);
	endif

	if ( GetObjProperty(me,"#heal") > ReadGameClock())
		return 0;
	endif

	SetObjProperty(me,"#heal", ReadGameClock()+20);

	if (me.poisoned) say("I'm poisoned!"); endif

	var bandage := FindMyBandage();
	if (!bandage)
		say("I don't have any bandages!");
		return 1;
	endif

	if (who.poisoned)
		CurePoison(me,0,me.name + " has CURED you!");
		say("*Whew!*  I'm cured!");
		return 1;
	endif

	var parms := {};
	parms[1] := me;
	parms[2] := (20 - CInt(GetEffectiveSkill(me,SKILLID_ANATOMY)/10));
	parms[3] := bandage;
	parms[4] := who;

	start_script(":healing:bandageheal",parms);

	return 1;

endfunction

function FindMyBandage()

	foreach item in EnumerateItemsInContainerOfObjtype(me.backpack, 0x0e21 )
		return item;
	endforeach
	return 0;

endfunction

function WearItem(it, you)

	var myhorse := GetEquipmentByLayer( me, 25 );

	var mygear := {};
	foreach thing in ListEquippedItems(me)
		if ( RemoveIt(thing) ) mygear.append(thing); endif
	endforeach

	set_critical(1);
	foreach thing in mygear
		if ( RemoveIt(thing) )
			MoveItemToContainer(thing, me.backpack);
		endif
	endforeach

	if (!EquipItem(me, it))
		say("I can't wear that.  Maybe I'm not strong enough.");
	endif

	foreach thing in mygear
     	  	if (!EquipItem(me, thing))
			MoveItemToContainer(thing,you.backpack);
		endif
	endforeach

	set_critical(0);

endfunction

function DropEverything()

	foreach item in ListRootItemsInContainer(me.backpack)
		MoveItemToLocation(item, me.x, me.y, me.z, 0);
	endforeach

endfunction

function MountHorse(horse)

	if (horse.script != "tamed")
		say("I don't know... looks kind of wild, to me.");
		return;
	elseif ( GetObjProperty(horse,"master") != masterserial )
		say("Howzabout you lemme ride YOUR horse?");
		return;
	endif

	var mounttype := 0;
	case (me.graphic)
	0xcc: //dark brown/horse
			mounttype := 0x3ea2;
	0xc8: //lightbrown/horse2
			mounttype := 0x3e9f;
	0xe2: //light grey/horse3
			mounttype := 0x3ea0;
	0xe4: //greybrown/horse4
			mounttype := 0x3ea1;
	0xdc: //Llama:
			mounttype := 0x3ea6;
	0xd2: //desert
			mounttype := 0x3ea3;
	0xda: //frenzied
			mounttype := 0x3ea4;
	0xdb: //forest
			mounttype := 0x3ea5;
	0x23d: //vortex
			mounttype := 0x3ea7;
	endcase


	if (!mounttype)
		say("Well how am I supposed to right THAT?");
		return;
	endif

	if (mounttype)
		if ( MoveCharacterToLocation(me,horse.x,horse.y,horse.z,0) )
			set_critical(1);
			me.facing := horse.facing;
			var mount := CreateItemAtLocation(5288, 1176, 0, 0xf021,1);
			mount.color := horse.color;
			SetObjProperty(mount,"petname",horse.name);
			SetObjProperty(mount,"pethp",GetHp(horse));
			SetObjProperty(mount,"petmana",GetMana(horse));
			SetObjProperty(mount,"freetime",ReadGameClock()+1200);
			mount.graphic := mounttype;
			MoveCharacterToLocation(horse, 5288, 1176, 0, MOVECHAR_FORCELOCATION);
			RevokePrivilege( horse, "invul" );
			SetObjProperty(horse,"guardkill",1);
			Damage(horse,GetMaxHp(horse)+3);
			EquipItem(me,mount);

			if (horse)
				MoveCharacterToLocation(horse, 5288, 1176, 0,
				 MOVECHAR_FORCELOCATION);
				RevokePrivilege(horse, "invul" );
				SetObjProperty(horse,"guardkill",1);
				Damage(horse,GetMaxHp(horse)+3);
			endif
			set_critical(0);
		endif
	endif

endfunction

function OpenMyPack()

	if (!master)
		master := SystemFindObjectBySerial( masterserial );
		me.setmaster( master );
		following := master;
		guarding := master;
	endif

	if (Distance(me,master) > 1)
		return;
	endif

	if (!me.backpack)
		var newbackpack := CreateItemAtLocation(5288, 1176, 0, 0xe75,1);
		EquipItem(me, newbackpack);
	endif

	SendOpenSpecialContainer( master, me.backpack );

endfunction

function GainStat()

	if ( laststrengthgain > ReadGameClock() )
		return;
	else
		laststrengthgain := ReadGameClock()+Random(120);
	endif

	if (!master) return; endif

	var leadership := GetEffectiveSkill(master,SKILLID_ENTICEMENT);

	var maxstr := GetStrength(master) - GetStrengthMod(master);
	var maxint := GetIntelligence(master) - GetIntelligenceMod(master);
	var maxdex := GetDexterity(master) - GetDexterityMod(master);
	if ( maxstr > leadership ) maxstr := leadership; endif
	if ( maxint > leadership ) maxint := leadership; endif
	if ( maxdex > leadership ) maxdex := leadership; endif

	var basestr := GetStrength(me) - GetStrengthMod(me);
	var baseint := GetIntelligence(me) - GetIntelligenceMod(me);
	var basedex := GetDexterity(me) - GetDexterityMod(me);

	if ( basestr < maxstr )
		if ( (Random(110)+1 ) > (basestr) )
			SetBaseStrength(me,basestr + 1);
			say("Woohoo!  I'm getting stronger!");
		endif
	endif
	if ( baseint < maxint )
		if ( (Random(110)+1 ) > (baseint) )
			SetBaseIntelligence(me,baseint + 1);
			say("I'm learning a lot from you!");
		endif
	endif
	if ( basedex < maxdex )
		if ( (Random(110)+1 ) > (basedex) )
			SetBaseDexterity(me,basedex + 1);
			say("Ha!  I liked that move!");
		endif
	endif

endfunction

function RunRandom(opponent, numruns := 2)

	var stepstotake := (numruns)+4;
	var tox := me.x;
	var toy := me.y;
	var highx := tox - opponent.x;
	if ( highx < 0 ) highx := 0 - highx; endif
	var highy := tox - opponent.y;
	if ( highy < 0 ) highy := 0 - highy; endif

	case ( highy > highx )
	0:	//run along the x access
		case ( Random(2) )
		0:	//to the right
			toy := (me.y - stepstotake);
			if (Random(2)) tox := opponent.x; endif //to his x
		1:	//to the left
			toy := (me.y + stepstotake);
			if (Random(2)) tox := opponent.x; endif //to his x
		endcase
	1:
		//run along the y access
		case( Random(2) )
		0:	//to the right
			tox := (me.x - stepstotake);
			if (Random(2)) toy := opponent.y; endif //to his y
		1:	//to the left
			tox := (me.x + stepstotake);
			if (Random(2)) toy := opponent.y; endif //to his y
		endcase
	endcase

	var runs := 0;
	var ev;
	while ( (Distance(me, opponent) > 0) && (runs <= stepstotake) )
		RunTowardLocation( tox, toy );
		OpenDoors(opponent);
		runs := runs +1;
		ev := wait_for_event(0);
		if (ev)
			case (ev.type)
			EVID_PEACEMADE: return 1;
			SYSEVENT_DAMAGED:
				AddToHitList(opponent);
				if (ev.source.serial == opponent.serial)
					Flee(opponent);
				endif
			endcase
		endif
		if ( (me.x == tox) && (me.y == toy) )
			return 0;
		endif
	endwhile

	return 0;

endfunction

function OpenDoors(opponent)

	//only check once per 5 seconds:
	var nextbreath := GetObjProperty(me,"#lastbreath");
	if ( (nextbreath) && (nextbreath > ReadGameClock() ) )
		return;
	endif

	if ( Distance(me,opponent) < 2 )
		SetObjProperty(me,"#lastbreath",ReadGameClock() + 20 );
		return;
	endif

	var itemdesc := ReadConfigFile( ":doors:itemdesc" );
	var doorstoopen := 0;
	var tox, toy;
	var doors := ListItemsNearLocation( me.x,me.y,me.z,3 );
	foreach door in doors
			if (itemdesc[door.objtype].doortype)
				doorstoopen := 1;
				tox := door.x;
				toy := door.y;
				break;
			endif
	endforeach

	if ( doorstoopen )
		start_script("::/misc/npcdoor", me);
		SetObjProperty(me,"#lastbreath",ReadGameClock() + 20 );
		sleepms(150);
		RunTowardLocation(tox, toy);
		sleepms(150);
		RunTowardLocation(tox, toy);
	else
		SetObjProperty(me,"#lastbreath",ReadGameClock() + 5 );
	endif

endfunction

function Release()

	me.setmaster( 0 );
	SetObjProperty(me, "master", 0);
	EraseObjProperty(master,"henchman");
	say("See ya");
	Flee(master);
	SetAnchor(me.x,me.y,10,1 );
	RestartScript(me);

endfunction

function Undress()

	say("Wooh!");

	set_critical(1);
	foreach thing in ListEquippedItems(me)
		if (RemoveIt(thing))
			MoveItemToContainer(thing, me.backpack);
		endif
	endforeach
	set_critical(0);

endfunction

function RemoveIt(it)

	if ( it == GetEquipmentByLayer( me, LAYER_BEARD ) )
		return 0;
	elseif ( it == GetEquipmentByLayer( me, LAYER_HAIR ) )
		return 0;
	elseif ( it == GetEquipmentByLayer( me, LAYER_MOUNT ) )
		return 0;
	elseif ( it == GetEquipmentByLayer( me, LAYER_HAIR ) )
		return 0;
	elseif ( it.objtype == 0x204e)
		return 0;
	elseif ( it == (me.backpack) )
		return 0;
	endif

	return 1;

endfunction

function DrinkHeal()

	if ( CInt(GetObjProperty(me, "#potion")) > ReadGameClock()  )
        	return 0;
	endif

	foreach item in EnumerateItemsInContainer(me.backpack)
		if ( ( item.objtype == 0xdc03 ) || ( item.objtype == 0xdc02 ) )
			PlaySoundEffect(me, DRINK_SOUND);
			PerformAction(me, ANIM_EAT);
			CreateItemInBackpack(me, UOBJ_EMPTY_BOTTLE, 1);
			var heal_amount;
			if (item.objtype == 0xdc02)
				heal_amount := RandomDiceStr("2d12+3");
			else
				heal_amount := RandomDiceStr("5d12+3");
			endif		
			HealDamage(me, heal_amount);
			DestroyItem(item);
			PlaySoundEffect( me,  SOUND_EFFECT_HEAL );
			SetObjProperty(me, "#potion", (ReadGameClock() + 20));
			return 1;
		endif
	endforeach

	return 0;

endfunction