use os;
use uo;
use npc;
use util;
use cfgfile;

include "include/utility";
include "include/attributes";
include "include/sysevent";
include "include/eventid";
include "include/randname";
include "include/attributes";
include "../pkg/opt/auctioneer/auctioneer";

var me := Self();
var next_possible_spam := ReadGameClock();

program auctioneer_ai()
	if (!me.backpack)
		DoAuctioneerOnCreationSetup();
	endif

	EnableEvents(SYSEVENT_ENGAGED + SYSEVENT_DAMAGED, 20);
	EnableEvents(SYSEVENT_ITEM_GIVEN + SYSEVENT_SPEECH + SYSEVENT_ENTEREDAREA, 2);

	var ev;
	while (me)
		if (me.hidden)
			sleep(30);
			me.hidden := 0;
		endif
	
		ev := wait_for_event(120);
		CheckCompletedAuctions();
		if (ev)
			ProcessEvent(ev);
		endif
	endwhile
endprogram




///////////////////
//  pretty obvious - processes events picked up in the main AI loop
///////////////////

function ProcessEvent(byref ev)
	case (ev.type)
		SYSEVENT_DAMAGED:
			me.hidden := 1;
			SetHp(me, me.maxhp);
		SYSEVENT_ENTEREDAREA:
			if (ev.source.isA(POLCLASS_NPC))
				return 0;
			endif

			if (ReadGameClock() > next_possible_spam)
				GreetCustomer(ev.source);
				next_possible_spam := ReadGameClock() + 60;
			endif
		SYSEVENT_SPEECH:
			if (ev.source.isA(POLCLASS_NPC))
				return 0;
			endif

			TurnToward(ev.source);
			var evtext := lower(ev.text);
			if (evtext["buy"])
				var parms := array;
				parms[1] := me;
				parms[2] := ev.source;
				start_script(":auctioneer:buyitems", parms);
			elseif (evtext["sell"])
				say("To sell, just drop items on me.");
			elseif (evtext["status"])
				var gold_held  := DetermineGoldHeld(ev.source.acctname);
				var gold_available := DetermineGoldAvailable(ev.source.acctname);
				PrintTextAbovePrivate(me, "I'm holding " + gold_held + " gold for you.", ev.source);
				sleep(1);
				PrintTextAbovePrivate(me, "Of that,  " + gold_available + " gold is available for your use.", ev.source);
			elseif (evtext["collect"])
				var gold_available  := DetermineGoldAvailable(ev.source.acctname);
				if (gold_available > 60000)
					gold_available := 60000;
				endif
			
				var gold_held := DetermineGoldHeld(ev.source.acctname);
				gold_held := gold_held - gold_available;
			
				if (!gold_available && gold_held)
					PrintTextAbovePrivate(me, "I have " + gold_held + " gold of yours, but I'm afraid I can't give it to you right now.", ev.source);
				elseif (!gold_available)
					PrintTextAbovePrivate(me, "What are you talking about?  I don't have your gold!", ev.source);
				elseif (CreateItemInContainer(ev.source.backpack, UOBJ_GOLD_COIN, gold_available) )
					AddAmountToGoldHeld(ev.source.acctname, (0-gold_available));
					if (gold_held)
						PrintTextAbovePrivate(me, "There you go.  I still have " + gold_held + " gold of yours.", ev.source);
					else
						PrintTextAbovePrivate(me, "There you go.  I don't have any more of your gold.", ev.source);
					endif
				else
					PrintTextAbovePrivate(me, "Maybe you should clean out your backpack some...", ev.source);
				endif
			endif

		SYSEVENT_MERCHANT_BOUGHT:
			TurnToward(ev.source);
			return 1;

		SYSEVENT_ITEM_GIVEN:
			TurnToward(ev.source);
			if (ev.item.objtype == UOBJ_GOLD_COIN)
				say("I'll add that to your account.");
				AddAmountToGoldHeld(ev.source.acctname, ev.item.amount);
				DestroyItem(ev.item);
				sleep(1);
				say("I'm currently holding " + DetermineGoldHeld(ev.source.acctname) + " gold for you.");
				return;
			endif
		
			var parms := array;
			parms[1] := me;
			parms[2] := ev.source;
			parms[3] := ev.item;
			start_script(":auctioneer:sellitems", parms);
	endcase
	return 1;
endfunction




///////////////////
//  when a player enters the NPCs range, this function makes them
//  yell a greeting
///////////////////

function GreetCustomer(customer)
	if (customer.isA(POLCLASS_NPC))
		return;
	endif

	if (customer.concealed)
		return;
	endif

	TurnToward(customer);
	case (RandomInt(10) )
		0:
			say("More fine clubs of macefighting than you can shake a stick at!");
		1:
			say("Welcome to my shop!");
		2:
			say("Let me know if you need help.");
		3:
			say("Let me show what I just got in!");
		4:
			say("I bet I have something you want!");
		5:
			say("Can I help you with anything?");
		6:
			say("Exotic merchandise from all over Britannia!");
		7:
			say(customer.name + "!  Its good to see you again.");
		8:
			say("Have anything you want to get rid of?  I'm your man!");
		9:
			say("Ah, " + customer.name + ", one of my best customers returns!");
	endcase
endfunction




///////////////////
//  called when the NPC is first created, this function names
//  them and clothes them
///////////////////

function DoAuctioneerOnCreationSetup()
	var newpack := CreateItemAtLocation(5288, 1176, 0, UOBJ_BACKPACK, 1);
	if (!EquipItem(me, newpack))
		DestroyItem(newpack);
	endif

	me.gender := RandomInt(2);
	me.graphic := 400+me.gender;
	me.name := RandomName(me);
	me.title_suffix := " the auctioneer";

	if (me.gender)
		EquipFromTemplate(me, "female_auctioneer");
	else
		EquipFromTemplate(me, "male_auctioneer");
	endif

	var myhome := {};
	myhome[1] := me.x;
	myhome[2] := me.y;
	myhome[3] := me.z;
	SetObjProperty(me, "myhome", myhome);
endfunction