/////////////////////////////////////////////////////
//
//  ".mtele" - multiple teleport
//
//     like .tele, but after teleporting, gives you
//     another teleport cursor, until canceled.
//     can be used for fast transportation
//
/////////////////////////////////////////////////////
use uo;
use os;

program textcmd_tele( who )
    SendSysMessage( who, "Where do you want to teleport?" );

    while (1)
        var where := TargetCoordinates( who );
        if (where)
            var result := MoveCharacterToLocation( who, where.x, where.y, where.z, MOVECHAR_FORCELOCATION );
	    showflame(who);
            if (!result)
                SendSysMessage( who, result.errortext );
            endif
        else
            break;
        endif
    endwhile
endprogram

function showflame(who)
  var coolflame;

  if ((who.cmdlevel>3) && (who.concealed!=1))
    coolflame := CreateItemAtLocation( who.x, who.y, who.z, 0x3709, 1 );
    PlaySoundEffect(who, 0x208);
    sleep(3);
    DestroyItem(coolflame);
  endif

endfunction