use os;
use uo;

include "include/objtype";
include "include/spelldata";
include "include/areas";
include "include/client";

program spell_gate( caster )

	var circle := 7;

	if( IsForbiddenAreaToGateFrom( caster ) )
		SendSysMessage( caster, "You can't gate from there." );
		return 0;
	endif

	if( caster.multi && GetObjProperty( caster.multi, "ownerserial" ) != caster.serial )
		var co_owners := {};
		var friends := {};
		var i;
		for( i := 1; i <= 5; i := i + 1 )
			if( GetObjProperty( caster.multi, "Co-owner" + i ) )
				co_owners.append( GetObjProperty( caster.multi, "Co-owner" + i ));
			endif
		endfor
	
		for( i := 1; i <= 20; i := i + 1 )
			if( GetObjProperty( caster.multi, "friend" + i ) )
				friends.append( GetObjProperty( caster.multi, "friend" + i ));
			endif
		endfor
		
		if( !( caster.serial in co_owners ) && !( caster.serial in friends ) )
			SendSysMessage( caster, "You can't gate from there!" );
			return;
		endif
	endif

	SendSysMessage( caster, "Select a rune to gate from." );
	var cast_on := CanTargetThing(caster, circle, TGTOPT_CHECK_LOS );
	if (!cast_on)
		return;
	elseif( !Accessible( caster, cast_on ) )
		SendSysMessage( caster, "You can't reach that." );
		return 0;
	elseif( cast_on.objtype != UOBJ_RUNE && cast_on.objtype != 0x6100 )
		SendSysMessage( caster, "You can only cast that on a recall rune or a runebook!" );
		return 0;
	elseif( !ReserveItem( cast_on ) )
		return 0;
	endif

	var tox := CInt(GetObjProperty( cast_on, "x" ));
	var toy := CInt(GetObjProperty( cast_on, "y" ));
	var toz := CInt(GetObjProperty( cast_on, "z" ));

	if( !tox && !toy && !toz )
		SendSysMessage(caster, "That is a blank rune!");
		return;
	endif

	if( IsForbiddenAreaToGateTo( caster, tox, toy ) )
		SendSysMessage(caster, "The destination is a magic place with protection.");
		if( cast_on.objtype != 0x6100 )
			DestroyItem( cast_on );
		endif
		return;
	endif

	var multi := GetStandingHeight( tox, toy, toz ).multi;
	if( multi )
		if( GetObjProperty( multi, "ownerserial" ) != caster.serial )
			var co_owners := {};
			var friends := {};
			var i;
			for( i := 1; i <= 5; i := i + 1 )
				if( GetObjProperty( multi, "Co-owner" + i ) )
					co_owners.append( GetObjProperty( multi, "Co-owner" + i ));
				endif
			endfor
		
			if( !( caster.serial in co_owners )  )
				SendSysMessage( caster, "You can't gate to this house." );
				return;
			endif
		
			for( i := 1; i <= 20; i := i + 1 )
				if( GetObjProperty( multi, "friend" + i ) )
					friends.append( GetObjProperty( multi, "friend" + i ));
				endif
			endfor
			
			if( !( caster.serial in friends ) )
				SendSysMessage( caster, "You can't gate to this house." );
				return;
			endif
		endif
	endif

	set_critical(1);

	if( !caster )
		return;
	endif

	var gate1 := CreateItemAtLocation( caster.x, caster.y, caster.z, UOBJ_BLUE_MOONGATE, 1 );
	var gate2 := CreateItemAtLocation( tox, toy, toz, UOBJ_BLUE_MOONGATE, 1 );
	if( !gate1 || !gate2 || !caster )
		DestroyItem(gate1);
		DestroyItem(gate2);
        	return;
	endif

	SetObjProperty( gate1, "GateDestX", gate2.x );
	SetObjProperty( gate1, "GateDestY", gate2.y );
	SetObjProperty( gate1, "GateDestZ", gate2.z );
	SetObjProperty( gate2, "GateDestX", gate1.x );
	SetObjProperty( gate2, "GateDestY", gate1.y );
	SetObjProperty( gate2, "GateDestZ", gate1.z );

	ReleaseItem(cast_on);
	Detach();

	set_critical(0);

	PlaySoundEffect( caster, SFX_SPELL_GATE_TRAVEL );

	sleep(30);
   
	set_critical(1);
	DestroyItem(gate1);
	DestroyItem(gate2);
	set_critical(0);

endprogram