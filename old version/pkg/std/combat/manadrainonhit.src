use uo;
use os;


include "include/client";
include "include/attributes";
include "include/damages";

program manadrainonhit( parms )

	var attacker 	 := parms[1];
	var defender 	 := parms[2];
	var weapon 	 := parms[3];
	var armor 	 := parms[4];
	var basedamage := parms[5];
	var rawdamage	 := parms[6];

	var absorbed 	 := basedamage - rawdamage;
	var drained;
	var drainer;

	ApplyTheDamage( defender, attacker, rawdamage );

	if( !absorbed )
		return;
	endif

	if( GetObjProperty( armor, "Cursed" ) )
		drained := defender;
		drainer := attacker;
	else
		drained := attacker;
		drainer := defender;
	endif

	var amount := absorbed;

	if( amount > GetMana(drained))
		SetMana(drained, 0);
	else
		SetMana(drained, GetMana(drained) - amount);
	endif

	SetMana(drainer, GetMana(drainer) + amount);
	if( GetMana(drainer) > GetMaxMana(drainer))
		SetMana(drainer, drainer.maxmana);
	endif


endprogram
