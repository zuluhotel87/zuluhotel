/*
        Maintened by *Edwards

        2008-06-01

        Last Modifications 2009-08-29
             * Added useItem_check()
*/
use uo;
use os;
use util;

include "include/uo_extend";
include "include/client";
include ":timeutils:time";
include ":security:itemReport";
include "include/itemUtil";

var sparkle_list := array( 0x1153, 0x373A, 0x374A, 0x375A, 0x376A, 0x3779 );
var lightning_sounds := array( 41, 42, 519 );
var explosion_list := array( 0x36B0, 0x36BD, 0x36CA );
var explosion_sounds := array( 520, 284, 285, 286, 287 );

program EatHeart( who, heart )

       	if( !useItem_check( who, heart, ITEM_INCONTAINER ))
		return 0;
	endif

        var last_usage := GetObjProperty( who, "HeartEvilLastUse" );
        if( last_usage && ( ReadGameClock() - last_usage ) < 2*SECONDS_IN_WEEK )
                SendSysMessage( who, "You must wait before eating an evil heart again" );
                return 0;
        else
                SetObjProperty( who, "HeartEvilLastUse", ReadGameClock() );
        endif

        if( GetObjProperty( who, "AutoRes" ))
                SendSysMessage( who, "You are magically forbidden to eat this heart again!" );
                return 0;
        elseif( len( ListItemsNearLocation( who.x, who.y, who.z, 6, who.realm )) > 1 );
                SendSysMessage( who, "You are magically forbidden to eat this heart here!" );
                return 0;
        endif
        
        ITEM_ReportText( who, "Eats a heartofevil", ITEM_REPORT_SYSLOG );
	DestroyItem( heart );
        
        SendSysMessage( who, "You eat the magic hearth!" );
        
        PerformAction( who, ANIM_EAT );
	case ( RandomInt(3) + 1 )
		1:
			PlaySoundEffect( who, 0x3b );
			break;
		2:
			PlaySoundEffect( who, 0x3c );
			break;
		3:
			PlaySoundEffect( who, 0x3d );
			break;
	endcase
 
        Sleep(2);
        who.paralyzed := 1;
        Sleep(2);
        
        CreateMagicCircle( who );
	LevitateUp( who );
        CreateCandles( who );

	foreach item in EnumeratePhysicalItems( who )
		if ( item != who.backpack )
			MoveItemToContainer( item, who.backpack );
		endif
                SleepMS(5);
	endforeach
	
	var i;
	for ( i:=0; i<=132; i+=1)
		if( RandomInt(5) == 2 )
				PlayLightningBoltEffect( who );
				DoLightningSound( who );
				DoSparkles( who );
                                PerformAction( who, ANIM_CAST_AREA );
		endif
		foreach player in ListMobilesInLineOfSight( who, 5 );
			if( RandomInt(8) == 2 )
				DoExplosion( player );
				DoExplosionSound( player );
			endif
			SleepMS(2);
		endforeach
		SleepMS(200);
		who.facing := i%8;
	endfor
 
        LevitateDown( who );
        
        PlayMovingEffectXYZ( who.x, who.y, who.z+75, who.x, who.y-1, who.z, 0x374a, 7, 14, 0, who.realm );
	PlaySoundEffect( who, 0x01f3 );
        Sleep(1);
        
	SendSysMessage( who, "Death pardons your next transgression into the nether realm!" );
        SetObjProperty( who, "AutoRes", 1 );
 
        who.paralyzed := 0;
        
        Sleep( 10 );
        
        foreach item in ListItemsNearLocation( who.x, who.y, who.z, 5, who.realm );
                if( GetObjProperty( item, "QuestItemDecay" ))
                        DestroyItem( item );
                endif
                SleepMS(2);
        endforeach
        
        return 1;
endprogram

function LevitateUp( who )

	var i:= who.z;
	var stop_height := who.z+20;
	for ( i:=who.z; i<=stop_height; i+=1 )
		MoveObjectToLocation( who, who.x, who.y, i, who.realm, MOVEOBJECT_FORCELOCATION );
		DoSparkles( who );
		SleepMS(200);
	endfor
endfunction

function LevitateDown( who )

	var i;
        var stop_height := who.z-20;
	for ( i:=who.z; i>=stop_height; i-=1 )
		MoveObjectToLocation( who, who.x, who.y, i, who.realm, MOVEOBJECT_FORCELOCATION );
		DoSparkles( who );
		SleepMS(200);
	endfor
endfunction

function DoSparkles( who )

	var sparkle := sparkle_list[RandomInt( sparkle_list.size() )+1];
        PlayObjectCenteredEffect( who, sparkle, 10, 10 );
endfunction

function DoLightningSound( who )

	PlaySoundEffect( who, lightning_sounds[RandomInt( lightning_sounds.size() )+1] );
endfunction

function DoExplosion( player )

	var explosion := explosion_list[RandomInt( explosion_list.size() )+1];
        PlayObjectCenteredEffect( player, explosion, 10, 10 );
endfunction

function DoExplosionSound( player )

	PlaySoundEffect( player, explosion_sounds[RandomInt( explosion_sounds.size() )+1] );
endfunction

function CreateMagicCircle( who )
        
        var config := ReadConfigFile( ":itemutils:sets" );
        var elem := config["bloodpentagram"];
        if( elem.errortext )
		SysLog( "Could not find set name bloodpentagram for heartofevil. -->"+elem.errortext );
		return 0;
	endif

	foreach line in ( GetConfigStringArray( elem, "Item" ))
		line := SplitWords( line );
		var objtype := CInt( line[1] ),
                    graphic := CInt( line[2] ),
                    color := CInt( line[3] ),
		    x := CInt( line[4] ),
                    y := CInt( line[5] ) +1,
                    z := CInt( line[6] );

		var item := CreateItemAtLocation( who.x+x, who.y+y, who.z+z, objtype, 1, who.realm );
                item.movable := 0;
		item.graphic := graphic;
                item.color := color;
                SetObjProperty( item, "QuestItemDecay", 1 );
		SleepMS(2);
	endforeach
        
        return 1;
endfunction

function CreateCandles( who )

        var x := who.x,
            y := who.y,
            z := who.z-20,
            realm := who.realm,
            i;
        
             PerformAction( who, ANIM_CAST_AREA );
	i := CreateItemAtLocation( x, y-2, z, 0x1854, 1, realm );
             i.movable := 0;
	     SetName( i, "Magic Candle" );
             SetObjProperty( i, "QuestItemDecay", 1 );
	     PlaySoundEffect( i, 0x022b );
	     Sleep( 1 );

             PerformAction( who, ANIM_CAST_AREA );
	i := CreateItemAtLocation( x+3, y-1, z, 0x1854, 1, realm );
             i.movable := 0;
	     SetName( i, "Magic Candle" );
             SetObjProperty( i, "QuestItemDecay", 1 );
	     PlaySoundEffect( i, 0x022b );
	     Sleep( 1 );

             PerformAction( who, ANIM_CAST_AREA );
	i := CreateItemAtLocation( x+2, y+2, z, 0x1854, 1, realm );
             i.movable := 0;
	     SetName( i, "Magic Candle" );
             SetObjProperty( i, "QuestItemDecay", 1 );
	     PlaySoundEffect( i, 0x022b );
	     Sleep( 1 );

             PerformAction( who, ANIM_CAST_AREA );
	i := CreateItemAtLocation( x-1, y+3, z, 0x1854, 1, realm );
             i.movable := 0;
	     SetName( i, "Magic Candle" );
             SetObjProperty( i, "QuestItemDecay", 1 );
	     PlaySoundEffect( i, 0x022b );
	     Sleep( 1 );

             PerformAction( who, ANIM_CAST_AREA );
	i := CreateItemAtLocation( x-2, y, z, 0x1854, 1, realm );
             i.movable := 0;
	     SetName( i, "Magic Candle" );
             SetObjProperty( i, "QuestItemDecay", 1 );
	     PlaySoundEffect( i, 0x022b );
	     Sleep( 1 );

        return 1;
endfunction
