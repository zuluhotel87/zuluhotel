/*
        Maintened by *Edwards

        Thanks to ZuluHotel and his developers for this original Idea!
*/
use os;
use uo;
use util;

include ":damage:damage";
include ":warMachine:explosion";

program warMachine_cannon( mobile, cannon )

        if( !GetObjProperty( cannon, "working" ))
                 SendSysMessage( mobile, "This cannon cannot be used." );
                 return 0;
        endif

        if( GetObjProperty( cannon, "#inuse" ))
                 SendSysMessage( mobile, "Cannon is in use." );
                 return 0;
        endif

        var powder,
            location,
            cannonball;

        if( !GetObjProperty( cannon, "#powder" ))
                 SendSysMessage( mobile, "Select a power keg." );
                 powder := Target( mobile, TGTOPT_CHECK_LOS );
                 if( powder.objtype != 0xe7f)
                          SendSysMessage( mobile, "Canceled." );
                          return 0;
                 endif
                 
                 if( Distance( mobile, powder ) > 2 )
	                  SendSysMessage( mobile, "This is too far." );
	                  return 0;
                 endif
                 
                 DestroyItem( powder );
                 SetObjProperty( cannon, "#powder", 1 );
                 PrintTextAbove( cannon, "*powder loaded*" );
        endif

        if( !GetObjProperty( cannon, "#cannonball" ))
                 SendSysMessage( mobile, "Select a cannon ball." );
                 cannonball := Target( mobile, TGTOPT_CHECK_LOS );
                 if( cannonball.objtype != 0xe73 )
                          SendSysMessage( mobile, "Canceled." );
                          return 0;
                 endif

                 if( Distance( mobile, cannonball ) > 2 )
	                  SendSysMessage( mobile, "This is too far." );
	                  return 0;
                 endif
                 
                 DestroyItem( cannonball );
                 PrintTextAbove( cannon, "*cannonball loaded*" );
                 SetObjProperty( cannon, "#cannonball", 1 );
        endif

        SetObjProperty( cannon, "#inuse", 1 );
        
        SendSysMessage( mobile, "Select Target" );
        location := TargetCoordinates( mobile );
        if( !location )
                 SendSysMessage( mobile, "Canceled." );
                 EraseObjProperty( cannon, "#inuse" );
                 return 0;
        endif

        FireWorks( mobile, cannon, location, "Cannon" );

        EraseObjProperty( cannon, "#powder" );
        EraseObjProperty( cannon, "#cannonball" );
        EraseObjProperty( cannon, "#inuse" );
        
        return 1;
endprogram
