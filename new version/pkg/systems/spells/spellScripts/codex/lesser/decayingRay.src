/*
        Maintened by *Edwards

        2008-12-10

        Last Modifications 2010-04-27
             * Cleaned codes for better performances
        
        Original Idea from ZuluHotel
*/
use uo;
use os;

include ":spells:spells";
include ":timedscripts:timedScripts";

program SpellScript_DecayingRay( params )

	var mobile := params[1];
	var info := params[2];
	var targ := params[3];
        var circle := SPELL_GetCircle( info.spell_id );
	params := 0;

	if( !SPELL_MobileCheck( mobile, targ ))
		return 0;
	elseif( GetObjProperty( targ, "ReactiveArmorMod" ))
                SendSysMessage( mobile, "The victim is protected from the spell effect." );
		return 0;
	endif

	SPELL_PlaySpellSFX( info.spell_id, targ );
	SPELL_PlaySpellGFX( info.spell_id, targ, mobile );

        if( SPELL_Reflected( mobile, targ, circle ) == "Reflected" )
                SPELL_PlaySpellSFX( info.spell_id, mobile );
	        SPELL_PlaySpellGFX( info.spell_id, mobile, targ );

                targ := mobile;
        endif

	var amount := SPELL_GetResist( mobile, targ, circle, SPELL_GetAmount( mobile )),
	    duration := SPELL_GetDuration( mobile );

        var protection := SPELL_GetImmunity( mobile, targ, circle );
	if( protection == "Immuned" )
		return 0;
        elseif( protection == "Cursed" )
                amount *= 1.50;
                duration *= 1.50;
	endif

        SendSysMessage( targ, "Your armor rating was reduced by "+amount+"." );
        
	TS_StartTimer( targ, "ReactiveArmorMod", duration, -amount, mobile );

	return 1;
endprogram
