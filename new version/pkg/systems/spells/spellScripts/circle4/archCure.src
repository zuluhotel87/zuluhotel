/*
        Maintened by *Edwards

        2008-10-26

        Last Modifications 2010-04-27
             * Cleaned codes for better performances
*/
use uo;
use os;

include ":spells:spells";
include ":timedscripts:timedScripts";

program SpellScript_ArchCure( params )

	var mobile := params[1];
	var info := params[2];
	//var targ := params[3];
        //var circle := SPELL_GetCircle( info.spell_id );
	params := 0;

	var area_size := CInt( AP_GetSkill( mobile, MAGERY ) / 25 ) + 1,
	    targs := SPELL_GetValidTargets( mobile, area_size );
 
	foreach vtarg in targs
                SleepMS(5);
                
                if( !SPELL_MobileCheck( mobile, vtarg ))
			continue;
                elseif( !CheckLineOfSight( mobile, vtarg ))
			continue;
                endif
                
                if( vtarg.poisoned )
		        var current_poison := CInt( GetObjProperty( vtarg, "#PoisonedLevel" )),
	   	            bonus := GetMobileProtection( mobile, "HealingIncrease" ),
            	            difficulty := CInt((( current_poison * 15 ) + 60 ) - ( bonus * 10 ));

        	        difficulty /= ClasseBonus( mobile, MAGE );

        	        if( SkillCheck( mobile, MAGERY, CInt( difficulty ), "", ADV_DISABLE ) > 0 )
                	        SPELL_PlaySpellSFX( info.spell_id, vtarg );
			        SPELL_PlaySpellGFX( info.spell_id, vtarg, mobile );
			        TS_LowerDuration( vtarg, "DefaultPoison", -1 );
        	        endif
                endif
	endforeach

	return 1;
endprogram
