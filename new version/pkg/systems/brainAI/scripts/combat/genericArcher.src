/*
        Maintained by *Edwards

        2010-02-23

        Last Modifications 2010-02-24
             * Added hidden check "*Spings from hiding!*"
*/
use uo;
use os;

include ":brainAI:npcNerves";
include ":brainAI:npcCommands";
include ":brainAI:commonFunctions";
include ":attributes:attributes";
include ":weapons:weaponInfo";
include "include/facings";
include ":damage:damage";

var pathfind_ticker := 0;

program BrainNerve( params )

	var npc		:= params[1];
	var nerve_name	:= params[2];
	var event	:= params[3];
	var bsettings	:= params[4];
	var scripts	:= params[5];

	NPC_SetuptCombat( npc );

	var opponent := event.source;
	AI_SetOpponent( npc, opponent );
 
	var min_range := bsettings["MinRange"],
            max_range := bsettings["MaxRange"];

	while( npc )
		var dist := Distance( npc, opponent );
		if( DoneFighting( npc, opponent, dist, bsettings ))
			if( scripts.Exists( "EndFight" ))
				AI_StartNerve( npc, "EndFight", scripts["EndFight"].script, params );
			else
				AI_WarMode( npc, 0 );
			endif
			AI_EndNerve( npc, nerve_name );
                        break;
                elseif( ShouldFlee( npc, opponent, bsettings ))
			AI_WarMode( npc, 0 );
			AI_Move( npc, opponent.x, opponent.y, opponent.z, NEMOVE_AWAY, NEMOVE_RUN, 20 );
		elseif( dist < min_range )
			AI_Move( npc, opponent.x, opponent.y, opponent.z, NEMOVE_AWAY, NEMOVE_RUN, ( min_range-dist ));
		elseif( dist > max_range )
			AI_Move( npc, opponent.x, opponent.y, opponent.z, NEMOVE_TOWARD, NEMOVE_RUN, ( dist-max_range ));
		else
			npc.facing := GetFacing(npc.x, npc.y, opponent.x, opponent.y);
		endif

		SleepMS(50);
		AI_ClearThoughts( npc, CLR_NERVE );
	endwhile

        return 1;
endprogram
