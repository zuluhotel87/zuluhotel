/*
        Created by *Edwards

        2009-12-12
*/
use uo;
use os;
use guilds;

include "include/itemUtil";
include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":housing:commonTask";

const BUTTON_MAIN := 0xA0;
const BUTTON_INFO := 0xA1;
const BUTTON_MANAGMENT := 0xA2;

const BUTTON_ADDCO := 0xA3;
const BUTTON_REMOVECO := 0xA4;
const BUTTON_ADDFRIEND := 0xA5;
const BUTTON_REMOVEFRIEND := 0xA6;
const BUTTON_LISTFRIEND := 0xA7;
const BUTTON_EJECTPLAYER := 0xA8;
const BUTTON_BANPLAYER := 0xA9;
const BUTTON_UNBANPLAYER := 0xAa;

const BUTTON_CHANGENAME := 0xAB;
const BUTTON_TRANSFEROWNER := 0xAC;
const BUTTON_DEMOLISHHOUSE := 0xAD;
const BUTTON_CHANGEKEY := 0xAE;
const BUTTON_ITEMMANAGMENT := 0xAF;
const BUTTON_CUSTOMHOUSING := 0xB0;

var selection := 1;

program UseHouseDeed( parms )

        var mobile := parms[1],
            sign := parms[2];
            
       	if( !useItem_check( mobile, sign, DEFAULT_ITEM ))
		return 0;
	endif
            
        displaySign( mobile, sign );
            
	return 1;
endprogram

function displaySign( mobile, sign )

        var house_serial := GetObjProperty( sign, "house_serial" ),
            house := SystemFindObjectBySerial( house_serial );

        //Initialize the gump
        var gump := GFCreateGump();

        //Load Main Background
	GFResizePic( gump, 15, 100, 2600, 412, 300 );
        GFGumpPic( gump, 150, 60, 0x64 );
	GFResizePic( gump, 30, 160, 5100, 382, 25 );
 
        var house_name := house.GetHouseName();
        if( house_name )
                GFTextMid( gump, 232, 100, 0, 2100, house_name );
        endif

        displayBasicOptions( gump );

        case( selection )
              1: displayMain( gump, house );
                 break;
              2: displayInformation( gump );
                 break;
              3: displayManagment( mobile, gump, house );
                 break;
        endcase

        //And now we need to know the answer...
        var input := GFSendGump( mobile, gump );
        if( !input )
                 SendSysMessage( mobile, "Cancelled." );
                 return 0;
        endif

        if( !house.CanCommand( mobile ))
                 SendSysMessage( mobile, "You do not have access to these functions." );
                 return displaySign( mobile, sign );
        elseif( input[BUTTON_MAIN] )
                 selection := 1;
        elseif( input[BUTTON_INFO] )
                 selection := 2;
        elseif( input[BUTTON_MANAGMENT] )
                 selection := 3;
        elseif( input[BUTTON_ADDCO] )
                AddCo( mobile, house );
        elseif( input[BUTTON_REMOVECO] )
                RemoveCo( mobile, house );
        elseif( input[BUTTON_ADDFRIEND] )
                AddFriend( mobile, house );
        elseif( input[BUTTON_REMOVEFRIEND] )
                RemoveFriend( mobile, house );
        elseif( input[BUTTON_LISTFRIEND] )
                ListFriends( mobile, house );
        elseif( input[BUTTON_EJECTPLAYER] )
                EjectPlayer( mobile, sign );
        elseif( input[BUTTON_BANPLAYER] )
                BanPlayer( mobile, house, sign );
        elseif( input[BUTTON_UNBANPLAYER] )
                UnbanPlayer( mobile, house );
        elseif( input[BUTTON_CHANGENAME] )
                ChangeName( mobile, house, sign );
        elseif( input[BUTTON_TRANSFEROWNER] )
                TransferOwnership( mobile, house, sign );
                return 1;
        elseif( input[BUTTON_DEMOLISHHOUSE] )
                DemolishHouse( mobile, house );
                return 1;
        elseif( input[BUTTON_CHANGEKEY] )
                ChangeKey( mobile, house );
                return 1;
        elseif( input[BUTTON_ITEMMANAGMENT] )
                ItemManagment( mobile );
                return 1;
        elseif( input[BUTTON_CUSTOMHOUSING] )
                //SetHouseCustom( mobile, house );
                return 1;
        endif
        
        return displaySign( mobile, sign );
endfunction

function displayBasicOptions( byref gump )

	GFTextLine( gump, 90, 162, 1720, "Main" );
        if( selection == 1 )
                GFGumpPic( gump, 65, 162, 0xD1 );
        else
                GFAddButton( gump, 65, 162, 0xD0, 0xD1, GF_CLOSE_BTN, BUTTON_MAIN );
        endif

	GFTextLine( gump, 190, 162, 1720, "Information" );;
        if( selection == 2 )
                GFGumpPic( gump, 165, 162, 0xD1 );
        else
                GFAddButton( gump, 165, 162, 0xD0, 0xD1, GF_CLOSE_BTN, BUTTON_INFO );
        endif

	GFTextLine( gump, 310, 162, 1720, "Managment" );;
        if( selection == 3 )
                GFGumpPic( gump, 285, 162, 0xD1 );
        else
                GFAddButton( gump, 285, 162, 0xD0, 0xD1, GF_CLOSE_BTN, BUTTON_MANAGMENT );
        endif
 
endfunction

function displayMain( byref gump, house )

	GFTextLine( gump, 90, 190, 2100, "Owner:" );
        var owner := house.GetOwner().name;
        if( !owner )
                owner := "-   No owner   -";
        endif
	GFTextLine( gump, 175, 190, 1720, owner );

        GFTextLine( gump, 90, 210, 2100, "Co-Owner:" );
        var co_owner := house.GetCoOwner().name;
        if( !co_owner )
                co_owner := "-   No co-owner   -";
        endif
        GFTextLine( gump, 175, 210, 1720, co_owner );

	GFTextLine( gump, 90, 250, 2100, "Guild:" );
        var guild_serial := house.GetGuildStone(),
            guild_name := "";
            
        if( guild_serial )
                var guildstone := SystemFindObjectBySerial( guild_serial ),
                    guild_id := guildstone.GetGuild(),
                    guild := FindGuild( guild_id );
                    
                guild_name := guild.GetProp( "Name" );
        endif
        GFTextLine( gump, 150, 250, 1720, guild_name );
 
	GFTextLine( gump, 90, 270, 2100, "Visits:" );
	GFTextLine( gump, 150, 270, 1720, house.GetVisitsList().Size() );

	GFTextLine( gump, 90, 290, 2100, "Last Visited:" );
	GFTextLine( gump, 190, 290, 1720, house.GetLastVisit() );

        var max_locked := house.GetMaxLockDowns(),
            num_locked := CInt( GetObjProperty( house, "NumLockDowns" ));
            
	GFTextLine( gump, 90, 330, 2100, "Locked down items:" );
	GFTextLine( gump, 220, 330, 1720, num_locked+" / "+max_locked );

        var max_secure := house.GetMaxSecure(),
            num_secure := CInt( GetObjProperty( house, "NumSecure" ));
            
	GFTextLine( gump, 90, 350, 2100, "Secured containers:" );
	GFTextLine( gump, 220, 350, 1720, num_secure+" / "+max_secure );
 
	return gump;
endfunction

function displayInformation( byref gump )

	GFTextLine( gump, 80, 200, 2100, "Select a co-owner" );
	GFAddButton( gump, 55, 200, 0xD2, 0xD3, GF_CLOSE_BTN, BUTTON_ADDCO );
	GFTextLine( gump, 80, 220, 2100, "Remove a co-owner" );
	GFAddButton( gump, 55, 220, 0xD2, 0xD3, GF_CLOSE_BTN, BUTTON_REMOVECO );
 
	GFTextLine( gump, 270, 200, 2100, "Add a friend" );
	GFAddButton( gump, 245, 200, 0xD2, 0xD3, GF_CLOSE_BTN, BUTTON_ADDFRIEND );
	GFTextLine( gump, 270, 220, 2100, "Remove a friend" );
	GFAddButton( gump, 245, 220, 0xD2, 0xD3, GF_CLOSE_BTN, BUTTON_REMOVEFRIEND );
	GFTextLine( gump, 270, 240, 2100, "List of friends" );
	GFAddButton( gump, 245, 240, 0xD2, 0xD3, GF_CLOSE_BTN, BUTTON_LISTFRIEND );
 
	GFTextLine( gump, 140, 280, 2100, "Eject a player from the house" );
	GFAddButton( gump, 115, 280, 0xD2, 0xD3, GF_CLOSE_BTN, BUTTON_EJECTPLAYER );
	GFTextLine( gump, 140, 300, 2100, "Ban a player from the house" );
	GFAddButton( gump, 115, 300, 0xD2, 0xD3, GF_CLOSE_BTN, BUTTON_BANPLAYER );
	GFTextLine( gump, 140, 320, 2100, "Unban a player from the house" );
	GFAddButton( gump, 115, 320, 0xD2, 0xD3, GF_CLOSE_BTN, BUTTON_UNBANPLAYER );
 
	return gump;
endfunction

function displayManagment( mobile, byref gump, house )

	GFTextLine( gump, 110, 220, 2100, "Change the house name" );
	GFAddButton( gump, 85, 220, 0xD2, 0xD3, GF_CLOSE_BTN, BUTTON_CHANGENAME );
 
	GFTextLine( gump, 110, 240, 2100, "Transfer ownership of the house" );
	GFAddButton( gump, 85, 240, 0xD2, 0xD3, GF_CLOSE_BTN, BUTTON_TRANSFEROWNER );
 
        if( house.GetHouseDeedObjType() )
	        GFTextLine( gump, 110, 260, 2100, "Demolish the house and get a deed back" );
	        GFAddButton( gump, 85, 260, 0xD2, 0xD3, GF_CLOSE_BTN, BUTTON_DEMOLISHHOUSE );
        endif
        
	GFTextLine( gump, 110, 280, 2100, "Change the master key" );
	GFAddButton( gump, 85, 280, 0xD2, 0xD3, GF_CLOSE_BTN, BUTTON_CHANGEKEY );
 
	GFTextLine( gump, 110, 300, 2100, "House item managment" );
	GFAddButton( gump, 85, 300, 0xD2, 0xD3, GF_CLOSE_BTN, BUTTON_ITEMMANAGMENT );

        //if( mobile.cmdlevel >= 5 )
		GFTextLine( gump, 110, 320, 2100, "Custom Housing" );
		GFAddButton( gump, 85, 320, 0xD2, 0xD3, GF_CLOSE_BTN, BUTTON_CUSTOMHOUSING );
        //endif
 
	return gump;
endfunction
